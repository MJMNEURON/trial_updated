begintemplate RelayCell
public soma, dend1, dend2, dend3, dend4, dend5, dend6, dend7, dend8, dend9, dend10, dend11

xopen("cells/tc200.geo") // read geometry file
xopen("loc200.oc") // load procedures for localizing T-current

proc init(){
	corrD = 1 // no dendritic surface correction
	G_pas = 3.79e-5
	E_pas = -73 // to fit current-clamp data (was -71 to -73)
	E_pas = -76.5 // within 3 mV error

	forall { // insert passive current everywhere
		insert pas
		g_pas = G_pas * corrD
		e_pas = E_pas
		cm = 0.88 * corrD
		Ra = 173
		L = L
	}

	soma {
		g_pas = G_pas
		cm = 0.88

		insert hh2 // insert fast spikes
		ena = 50
		ek = -100
		vtraub_hh2 = -52
		gnabar_hh2 = 0.1
		gkbar_hh2 = 0.1
	}

	forall {
		insert itGHK // T-current everywhere
		cai = 2.4e-4
		cao = 2
		eca = 120
		shift_itGHK = -1 // screening charge shift + 3 mV error
		gcabar_itGHK = corrD * 0.0002
		qm_itGHK = 2.5
		qh_itGHK = 2.5

		insert cad // calcium diffusion everywhere
		depth_cad = 0.1 * corrD
		kt_cad = 0 // no pump
		kd_cad = 1e-4
		taur_cad = 5
		cainf_cad = 2.4e-4
	}
	
	localize(1.7e-5,corrD*8.5e-5,corrD*8.5e-5)
}

endtemplate RelayCell
